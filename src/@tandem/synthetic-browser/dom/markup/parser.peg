{

const {
  MarkupTextExpression,
  HTMLScriptExpression,
  MarkupCommentExpression,
  MarkupElementExpression,
  MarkupAttributeExpression,
  MarkupFragmentExpression,
} = require("./ast");

function position() {
  return {
    start:peg$savedPos,
    end:peg$currPos
  };
}

}

Start
  = Fragment

Fragment
  = nodes:(Node/TextNode)* {
    return new MarkupFragmentExpression(nodes, position());
  }

Node
  = _ node:(Comment/VoidElement/Element/TextNode) _ { return node; }

Comment
  = '<!--' characters:CommentCharacter* '-->' { return new MarkupCommentExpression(characters.join(''), position()); }

CommentCharacter
  = !'-->' character:. { return character; }

VoidElement
  = "<" nodeName: VoidTagName attributes:ElementAttributes _ (">"/"/>")? endTag:EndVoidTag? {

    if (endTag && nodeName != endTag.name) {
      expected("</" + nodeName + ">");
    }

    return new MarkupElementExpression(nodeName, attributes, [], position());
  }

EndVoidTag
  = _ "</" name: VoidTagName ">" {
    return {
      name: name
    };
  }

VoidTagName
  = name:  ("area"/"base"/"br"/"col"/"command"/"embed"/"hr"/"img"/"input"/"keygen"/"link"/"meta"/"param"/"source"/"track"/"wbr")

Element
  = ElementWithoutChildNodes
  / ElementWithChildNodes

ElementWithoutChildNodes
  = startTag:ElementStart attributes:ElementAttributes _ '/>' {
    return new MarkupElementExpression(startTag, attributes, [], position());
  }

ElementWithChildNodes
  = startTag:ElementStart attributes:ElementAttributes '>' _ childNodes:Node* _ '</' endTag:Name '>' {
    if (startTag != endTag) {
      expected('</' + startTag + '>');
    }
    return new MarkupElementExpression(startTag, attributes, childNodes, position());
  }

ElementStart
  = '<' name:Name { return name; }

ElementAttributes
  = attributes:ElementAttribute* {
    return attributes;
  }

ElementAttribute
  = _ key:Name '=' value:AttributeValue {
    return new MarkupAttributeExpression(key, value, position());
  }
  / _ key:Name _ {
    return new MarkupAttributeExpression(key, "", position());
  }

AttributeValue
  = String

String
  = '"' characters:DoubleStringCharacter* '"' { return characters.join(''); }
  / "'" characters:SingleStringCharacter* "'" { return characters.join(''); }

DoubleStringCharacter
  = !('"' / "\\") . { return text(); }
  / '\\"'

SingleStringCharacter
  = !("'" / "\\") . { return text(); }
  / "\\'"

TextNode
  = [^<]+ {
    return new MarkupTextExpression(text(), position());
  }

Name
  = [a-z0-9$-.]+ { return text(); }
_
  = [ \n\r\t]*
